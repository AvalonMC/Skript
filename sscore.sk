on tab complete of "/sscore":
    set tab completions for position 1 to "get", "set", "add", "remove" and "delete"
    if tab arg-1 matches "get":
        set tab completions for position 2 to "value" and "list"
    else if tab arg-1 matches "set":
        set tab completions for position 2 to "string" and "number"
    else if tab arg-1 matches "delete":
        set tab completions for position 2 to "anything"
    else:
        set tab completions for position 2 to "number"
    set tab completions for position 3 to all players

function getSscore(playerName: string, identifier: string) :: string:
    if yml {_identifier} in file "plugins/Skript/scripts/players/%{_playerName}%.yml" doesn't exist:
        return "0"
    else:
        set {_return} to yml value {_identifier} from file "plugins/Skript/scripts/players/%{_playerName}%.yml"
        return "%{_return}%"

command /sscore <text> <text> <text> <text> [<text>]:
    permission: group.admin
    description: YAML-based scores
    trigger:
        if file "plugins/Skript/scripts/players/%arg-3%.yml" doesn't exist:
            create file "plugins/Skript/scripts/players/%arg-3%.yml"
        
        if arg-1 matches "get":
            if arg-2 matches "list":
                message yml list arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml" to command executor
            else:
                message getSscore(arg-3, arg-4) to command executor

        else if arg-1 matches "set":
            if arg-2 matches "number":
                set yml value arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml" to arg-5 parsed as number
            else if arg-2 matches "string":
                set yml value arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml" to arg-5

        else if arg-1 matches "add":
            set {_prevValue} to getSscore(arg-3, arg-4)
            set yml value arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml" to {_prevValue} parsed as number + arg-5 parsed as number

        else if arg-1 matches "remove":
            set {_prevValue} to getSscore(arg-3, arg-4)
            set yml value arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml" to {_prevValue} parsed as number - arg-5 parsed as number

        else if arg-1 matches "delete":
            delete yml value arg-4 from file "plugins/Skript/scripts/players/%arg-3%.yml"
            
        else:
            message "Wrong argument %arg-1%" to command executor
